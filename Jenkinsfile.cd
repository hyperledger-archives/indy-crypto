#!groovy

@Library('SovrinHelpers') _

try {
    testing()
    publishing()
    if (acceptanceTesting()) {
        releasing()
    }
    notifyingSuccess()
} catch (err) {
    notifyingFailure()
    throw err
}

def testing() {
    stage('Testing') {
        parallel([
                'ubuntu-test' : { ubuntuTesting() },
                'windows-test': { windowsTesting() }
        ])
    }
}

def publishing() {
    stage('Publishing') {
        echo "${env.BRANCH_NAME}: start publishing"

        publishedVersions = parallel([
                'ubuntu-files' : { ubuntuPublishing() },
                'windows-files': { windowsPublishing() },
        ])

        if (publishedVersions['windows-files'] != publishedVersions['ubuntu-files']) {
            error "platforms artifacts have different versions"
        }
    }
}

def acceptanceTesting() {
    stage('Acceptance testing') {
        if (env.BRANCH_NAME == 'rc') {
            echo "${env.BRANCH_NAME}: acceptance testing"
            if (approval.check("default")) {
                return true
            }
        } else {
            echo "${env.BRANCH_NAME}: skip acceptance testing"
        }
        return false
    }
}

def releasing() {
    stage('Releasing') {
        if (env.BRANCH_NAME == 'rc') {
            publishingRCtoStable()
        }
    }
}

def notifyingSuccess() {
    if (env.BRANCH_NAME == 'master') {
        currentBuild.result = "SUCCESS"
        node('ubuntu-master') {
            sendNotification.success('indy-crypto')
        }
    }
}

def notifyingFailure() {
    currentBuild.result = "FAILED"
    node('ubuntu-master') {
        sendNotification.fail([slack: env.BRANCH_NAME == 'master'])
    }
}

void getSrcVersion() {
    commit = sh(returnStdout: true, script: 'git rev-parse HEAD').trim()
    version = sh(returnStdout: true, script: "wget -q https://raw.githubusercontent.com/hyperledger/indy-crypto/$commit/libindy-crypto/Cargo.toml -O - | grep -E '^version =' | head -n1 | cut -f2 -d= | cut -f2 -d '\"'").trim()
    return version
}

def linuxTesting(file, env_name) {
    try {
        echo "${env_name} Test: Checkout csm"
        checkout scm

        def testEnv

        dir('libindy-crypto') {
            echo "${env_name} Test: Build docker image"

            testEnv = dockerHelpers.build('libindy-crypto', file)
            testEnv.inside {
                echo "${env_name} Test: Test"
                try {
                    echo "${env_name} Test: Build"
                    sh "RUST_BACKTRACE=1 cargo test --release --no-run"

                    echo "${env_name} Test: Run tests"
                    sh "RUST_BACKTRACE=1 RUST_LOG=trace cargo test --release"
                }
                finally {
                    /* TODO FIXME restore after xunit will be fixed
                    junit 'test-results.xml'
                    */
                }
            }
        }

        sh "cp libindy-crypto/target/release/libindy_crypto.so wrappers/python"
        dir('wrappers/python') {
            testEnv.inside() {
                echo "${env_name} Test: Test python wrapper"

                sh '''
                    python3.5 -m pip install --user -e .
                    LD_LIBRARY_PATH=./ RUST_LOG=trace python3.5 -m pytest
                '''
            }
        }
    }
    finally {
        step([$class: 'WsCleanup'])
    }
}

def windowsTesting() {
    node('win2016') {
        stage('Windows Test') {
            echo "Windows Test: Checkout scm"
            checkout scm

            try {
                dir('libindy-crypto') {
                    echo "Windows Test: Build"
                    withEnv([
                            "RUST_BACKTRACE=1"
                    ]) {
                        bat "cargo test --release --no-run"

                        echo "Windows Test: Run tests"
                        withEnv([
                                "RUST_TEST_THREADS=1",
                                "RUST_LOG=trace",
                        ]) {
                            bat "cargo test --release"
                        }
                    }
                }

                //TODO wrappers testing

            } finally {
                step([$class: 'WsCleanup'])
            }
        }
    }
}

def ubuntuTesting() {
    node('ubuntu') {
        stage('Ubuntu Test') {
            linuxTesting("ci/ubuntu.dockerfile ci", "Ubuntu")
        }
    }
}

def ubuntuPublishing() {
    node('ubuntu') {
        stage('Publish Ubuntu Files') {
            try {
                echo 'Publish Ubuntu files: Checkout csm'
                checkout scm

                version = getSrcVersion()

                echo 'Publish Ubuntu files: Build docker image'
                testEnv = dockerHelpers.build('indy-crypto', 'libindy-crypto/ci/ubuntu.dockerfile libindy-crypto/ci')

                libindyCryptoDebPublishing(testEnv, false, version)
                libindyCryptoCargoPublishing(testEnv, false)
                pythonWrapperPypiPublishing(testEnv, false)
                pythonWrapperDebPublishing(testEnv, false)
            }
            finally {
                echo 'Publish Ubuntu files: Cleanup'
                step([$class: 'WsCleanup'])
            }
        }
    }

    return version
}

def windowsPublishing() {
    node('win2016') {
        stage('Publish Libindy Crypto Windows Files') {
            try {
                echo 'Publish Windows files: Checkout csm'
                checkout scm

                version = getSrcVersion()

                dir('libindy-crypto') {
                    echo "Publish Windows files: Build"
                    withEnv([
                            "RUST_BACKTRACE=1"
                    ]) {
                        bat "cargo build --release"
                    }

                    withCredentials([file(credentialsId: 'SovrinRepoSSHKey', variable: 'sovrin_repo_key')]) {
                        sh "./ci/libindy_crypto-win-zip-and-upload.sh $version '${sovrin_repo_key}' $env.BRANCH_NAME $env.BUILD_NUMBER"
                    }
                }
            }
            finally {
                echo 'Publish Windows files: Cleanup'
                step([$class: 'WsCleanup'])
            }
        }
    }
    return version
}

def getSuffix(isRelease, target) {
    def suffix
    if (env.BRANCH_NAME == 'master' && !isRelease) {
        suffix = "-dev-$env.BUILD_NUMBER"
    } else if (env.BRANCH_NAME == 'rc') {
        if (isRelease) {
            suffix = ""
        } else {
            suffix = "-rc-$env.BUILD_NUMBER"
        }
    } else {
        error "Publish To ${target}: invalid case: branch ${env.BRANCH_NAME}, isRelease ${isRelease}"
    }
    return suffix
}

def libindyCryptoDebPublishing(testEnv, isRelease, version) {
    dir('libindy-crypto') {
        echo 'Publish Libindy-Crypto deb files to Apt'
        testEnv.inside('-u 0:0') {
            sh 'chmod -R 755 ci/*.sh'

            def suffix = getSuffix(isRelease, "Apt(Libindy-Crypto)")
            def type = env.BRANCH_NAME == 'rc' && isRelease ? 'stable' : $env.BRANCH_NAME

            dir('sovrin-packaging') {
                downloadPackagingUtils()
            }

            withCredentials([file(credentialsId: 'SovrinRepoSSHKey', variable: 'sovrin_repo_key')]) {
                try {
                    sh 'mkdir /root/.ssh && ln -sf $sovrin_repo_key /root/.ssh'
                    sh "./ci/libindy_crypto-deb-build-and-upload.sh $version $type $suffix"
                }
                finally {
                    sh "rm -rf debian"
                    sh 'rm -f ~/.ssh/$sovrin_repo_key'
                }
            }
        }
    }
}

def libindyCryptoCargoPublishing(testEnv, isRelease) {
    dir('libindy-crypto') {
        echo 'Publish Libindy-Crypto to Cargo'
        testEnv.inside {
            def suffix = getSuffix(isRelease, "Cargo")
            sh "sed -i -E -e 'H;1h;\$!d;x' -e \"s/version = \\\"([0-9,.]+)/version = \\\"\\1$suffix/\" Cargo.toml"

            withCredentials([string(credentialsId: 'cargoSecretKey', variable: 'LOGIN')]) {
                sh 'cargo login $LOGIN'
                sh 'cargo package --allow-dirty'
                sh 'cargo publish --allow-dirty'
            }
        }
    }
}

def pythonWrapperPypiPublishing(testEnv, isRelease) {
    dir('wrappers/python') {
        echo 'Publish Python Wrapper to Pypi'
        testEnv.inside {
            def suffix = getSuffix(isRelease, "Pypi")

            withCredentials([file(credentialsId: 'pypi_credentials', variable: 'credentialsFile')]) {
                sh 'cp $credentialsFile ./'
                sh "sed -i -E \"s/version='([0-9,.]+).*/version='\\1$suffix',/\" setup.py"
                sh '''
                    python3.5 setup.py sdist
                    python3.5 -m twine upload dist/* --config-file .pypirc
                '''
            }
        }
    }
}

def pythonWrapperDebPublishing(testEnv, isRelease) {
    dir('wrappers/python') {
        echo 'Publish Python Wrapper deb files to APt'
        testEnv.inside {
            sh 'chmod -R 755 ci/*.sh'
            def suffix = getSuffix(isRelease, "Apt(Python wrapper)")
            def type = env.BRANCH_NAME == 'rc' && isRelease ? 'stable' : $env.BRANCH_NAME

            dir('sovrin-packaging') {
                downloadPackagingUtils()
            }

            withCredentials([file(credentialsId: 'SovrinRepoSSHKey', variable: 'sovrin_repo_key')]) {
                try {
                    sh 'mkdir /home/indy/.ssh && ln -sf $sovrin_repo_key ~/.ssh'
                    sh "./ci/python-wrapper-deb-build-and-upload.sh $type $suffix"
                }
                finally {
                    sh 'rm -f ~/.ssh/$sovrin_repo_key'
                }
            }
        }
    }
}

def publishingRCtoStable() {
    node('ubuntu') {
        stage('Moving RC artifacts to Stable') {
            try {
                echo 'Moving RC artifacts to Stable: Checkout csm'
                checkout scm

                version = getSrcVersion()

                echo 'Moving Windows RC artifacts to Stable: libindy-crypto'
                publishLibindyCryptoWindowsFilesRCtoStable(version)

                echo 'Moving RC artifacts to Stable: Build docker image'
                testEnv = dockerHelpers.build('indy-crypto', 'libindy-crypto/ci/ubuntu.dockerfile libindy-crypto/ci')

                echo 'Moving Ubuntu RC artifacts to Stable: libindy-crypto'
                libindyCryptoDebPublishing(testEnv, true, version)

                echo 'Moving RC artifacts to Stable: libindy-crypto to Cargo '
                libindyCryptoCargoPublishing(testEnv, true)

                echo 'Moving RC artifacts to Stable: python wrapper to Pypi '
                pythonWrapperPypiPublishing(testEnv, true)

                echo 'Moving RC artifacts to Stable: python wrapper deb files '
                pythonWrapperDebPublishing(testEnv, true)
            } finally {
                echo 'Moving RC artifacts to Stable: Cleanup'
                step([$class: 'WsCleanup'])
            }
        }
    }
}

def publishLibindyCryptoWindowsFilesRCtoStable(version) {
    rcFullVersion = "${version}-${env.BUILD_NUMBER}"
    withCredentials([file(credentialsId: 'SovrinRepoSSHKey', variable: 'sovrin_repo_key')]) {
        src = "/var/repository/repos/windows/libindy_crypto/rc/$rcFullVersion/"
        target = "/var/repository/repos/windows/libindy_crypto/stable/$version"

        sh "ssh -v -oStrictHostKeyChecking=no -i '$sovrin_repo_key' repo@192.168.11.115 '! ls $target'"
        sh "ssh -v -oStrictHostKeyChecking=no -i '$sovrin_repo_key' repo@192.168.11.115 cp -r $src $target"
    }
}

def downloadPackagingUtils() {
    git branch: 'master', credentialsId: 'evernym-github-machine-user', url: 'https://github.com/evernym/sovrin-packaging'
}